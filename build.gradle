buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath("io.spring.gradle:dependency-management-plugin:$springPlatformGradleVersion")
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
	}
}

apply plugin: "io.spring.dependency-management"

dependencyManagement {
	imports {
		mavenBom "io.spring.platform:platform-bom:$springPlatformVersion"
	}
}

repositories {
	jcenter()
	mavenLocal()
}

apply plugin: "java-library"
apply plugin: "org.springframework.boot"
apply plugin: "maven"
apply plugin: "eclipse"

group = "accelerate"
version = "1.0"

task wrapper(type: Wrapper) {
	gradleVersion = gradleVersion
}

/*
 * Configuration
 */
project.ext.buildDate = new Date().format("yyyyMMdd");

/*
 * Dependencies
 */
dependencies {
	// compile dependencies
	api ("org.springframework.boot:spring-boot-starter")
	api ("org.springframework.boot:spring-boot-starter-aop")
	implementation ("org.apache.commons:commons-lang3")
	//implementation ("commons-io:commons-io")
	implementation ("com.fasterxml.jackson.core:jackson-databind")
	compileOnly ("org.springframework:spring-jdbc")
	compileOnly ("javax.servlet:javax.servlet-api")
	
	//test dependencies
	testImplementation ("org.springframework.boot:spring-boot-starter-test")
	testRuntimeOnly ("org.springframework.boot:spring-boot-devtools")
}

/*
 * Build Settings
 */
compileJava {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

compileTestJava {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

bootJar {
    classifier = "boot"
    manifest {
		attributes (
			"Start-Class": "N/A"
		)
	}
}

jar {
	enabled = true
	from sourceSets.main.allSource
	manifest {
		attributes (
			"version-info" : "{'accelerate-utils' : {'gradle-version' : '${gradleVersion}', " 
				+ "'java-version' : '${javaVersion}', 'spring-boot-version' : '${springBootVersion}', "
				+ "'spring-io-version' : '${springPlatformVersion}'}",
			"created": buildDate
		)
    }
}

build.finalizedBy install

/*
 * Eclipse settings
 */
eclipse {
	project {
		natures = ["org.eclipse.buildship.core.gradleprojectnature", 
			"org.springframework.ide.eclipse.core.springnature", "org.eclipse.wst.common.project.facet.core.nature", 
			"org.eclipse.jdt.core.javanature"]
		
		buildCommand "org.eclipse.buildship.core.gradleprojectbuilder"
		buildCommand "org.springframework.ide.eclipse.core.springbuilder"
		buildCommand "org.springframework.ide.eclipse.boot.validation.springbootbuilder"
		buildCommand "org.eclipse.wst.common.project.facet.core.builder"
		buildCommand "org.eclipse.jdt.core.javabuilder"
	}
	
	jdt {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
		
		file {
			withProperties { properties ->
				properties.put("org.eclipse.jdt.core.compiler.processAnnotations", "disabled")
				properties.put("org.eclipse.jdt.core.compiler.codegen.targetPlatform", javaVersion)
			}
		}
	}
}